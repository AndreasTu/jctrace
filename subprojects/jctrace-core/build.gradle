plugins {
    id "com.github.johnrengelman.shadow"
}


// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    implementation deps.slf4j
    implementation deps.slf4jJdk

    implementation deps.asm

    //Currently not used in production
    implementation deps.disruptor

    implementation deps.gnuTrove
    compileOnly deps.jsr305


    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation deps.guava
}


test {
    jvmArgs '-Xmx1124m'

    //jvmArgs '-XX:+UnlockCommercialFeatures'
    //jvmArgs '-XX:+FlightRecorder'
    //jvmArgs '-XX:StartFlightRecording=dumponexit=true,settings=profile.jfc,filename=d:/test.jfr'

}


shadowJar {
    dependencies {
        exclude(dependency(deps.disruptor))
        exclude('module-info.class')
    }
    //ASM
    relocate 'org.objectweb.asm', 'de.turban.deadlock.tracer.dep.asm'

    //GNU Trove
    relocate 'gnu.trove', 'de.turban.deadlock.tracer.dep.gnu.trove'

    //slf4j
    relocate 'org/slf4j', 'de.turban.deadlock.tracer.dep.org.slf4j'


    from('../../LICENSES') {
        include '*.LICENSE'
        into('META-INF/LICENSES')
    }

    from('../../') {
        include 'LICENSE'
        into('META-INF')
    }
}

jar {
    from('../../LICENSES') {
        include '*.LICENSE'
        into('META-INF/LICENSES')
    }

    from('../../') {
        include 'LICENSE'
        into('META-INF')
    }

    manifest {

        attributes(

            'Implementation-Title': project.name,

            'Implementation-Version': project.version,
            'Can-Redefine-Classes': 'true',
            //'Main-Class': 'de.turban.deadlock.tracer.runtime.display.ui.DeadlockTracerUiMain',
            'Premain-Class': 'de.turban.deadlock.tracer.DeadlockTracerAgent'

        )
    }
}

apply from: 'runMeasurement.gradle'
