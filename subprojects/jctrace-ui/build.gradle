plugins{
    id 'application'
    id "org.openjfx.javafxplugin"
}

def mainClassName = 'de.turban.deadlock.tracer.runtime.display.ui.DeadlockTracerUiMain'

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation project(':jctrace-core')
    implementation libs.guava
    implementation libs.gnuTrove
}


javafx {
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application{
    mainClass = mainClassName
}

tasks.named("run").configure{
    if (hasDbFile()) {
        it.args rootProject.file(rootProject.ext.dbFile)
    }
    it.doFirst {
        assertDbFileSpecified()
    }
}

tasks.register("runReportXml", JavaExec) {
    group "Run"
    classpath = sourceSets.main.runtimeClasspath

    main = 'de.turban.deadlock.tracer.runtime.display.PrintMain'
    if (hasDbFile()) {
        it.args rootProject.file(rootProject.ext.dbFile)
    }
    if (hasReportFile()) {
        it.args rootProject.file(rootProject.ext.reportFile)
    }

    it.doFirst {
        assertDbFileSpecified()
        assertReportFileSpecified()
    }
}

tasks.named("jar", Jar).configure {
    manifest {
        attributes(
            'Main-Class': mainClassName,
        )
    }
}


/*
 * Helper methods
 */

boolean hasDbFile() {
    rootProject.hasProperty('dbFile')
}

boolean hasReportFile() {
    rootProject.hasProperty('reportFile')
}

void assertDbFileSpecified() {
    if (!hasDbFile()) {
        throw new IllegalArgumentException("You have to specify a db file with -PdbFile=pathToFile")
    }
}

void assertReportFileSpecified() {
    if (!hasReportFile()) {
        throw new IllegalArgumentException("You have to specify a report file with -PreportFile=pathToFile")
    }
}
