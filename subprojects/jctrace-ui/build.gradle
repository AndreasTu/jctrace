def mainClassName = 'de.turban.deadlock.tracer.runtime.display.ui.DeadlockTracerUiMain'

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation project(':subprojects:jctrace-core')
    implementation deps.guava
    implementation deps.gnuTrove
}


tasks.register("runUI", JavaExec) {
    group "Run"
    description "Starts the UI to display the database specified with -PdbFile=Deadlock.db"
    classpath = sourceSets.main.runtimeClasspath

    main = mainClassName
    if (hasDbFile()) {
        it.args rootProject.file(rootProject.ext.dbFile)
    }

    it.doFirst {
        assertDbFileSpecified()
    }
}

tasks.register("runReportXml", JavaExec) {
    group "Run"
    classpath = sourceSets.main.runtimeClasspath

    main = 'de.turban.deadlock.tracer.runtime.display.PrintMain'
    if (hasDbFile()) {
        it.args rootProject.file(rootProject.ext.dbFile)
    }
    if (hasReportFile()) {
        it.args rootProject.file(rootProject.ext.reportFile)
    }

    it.doFirst {
        assertDbFileSpecified()
        assertReportFileSpecified()
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Main-Class': mainClassName,
        )
    }
}


/*
 * Helper methods
 */

boolean hasDbFile() {
    rootProject.hasProperty('dbFile')
}

boolean hasReportFile() {
    rootProject.hasProperty('reportFile')
}

void assertDbFileSpecified() {
    if (!hasDbFile()) {
        throw new IllegalArgumentException("You have to specify a db file with -PdbFile=pathToFile")
    }
}

void assertReportFileSpecified() {
    if (!hasReportFile()) {
        throw new IllegalArgumentException("You have to specify a report file with -PreportFile=pathToFile")
    }
}
